buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "net.researchgate:gradle-release:$gradleReleaseVersion"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'kotlin-platform-common'
apply plugin: 'maven-publish'
apply plugin: 'net.researchgate.release'
apply plugin: 'org.jetbrains.dokka'

description = 'A Result monad for modelling success or failure operations.'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion"
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/docs"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Assembles a jar archive containing the Javadoc API documentation.'
    classifier = 'javadoc'
    from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Assembles a jar archive containing the main classes with sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
        }
    }
}
